IF(CONNECTOR_ISAPI)
  IF(NOT MULTI_THREADED)
    MESSAGE("** Disabling ISAPI because MULTI_THREADED is disabled.")
  ELSE(NOT MULTI_THREADED)
    MESSAGE("** Enabling ISAPI.")

    ADD_DEFINITIONS(-D_WIN32_WINNT=0x0501 -DWTISAPI_STATIC)

    SET(libsapisources
      Server.C
      WServer.C
      Isapi.C
      IsapiRequest.C
      IsapiStream.C
    )

    INCLUDE_DIRECTORIES(${FCGI_INCLUDE_DIRS})

    ADD_LIBRARY(wtisapi STATIC ${libsapisources})
    TARGET_LINK_LIBRARIES(wtisapi wt)

    SET_TARGET_PROPERTIES(
      wtisapi
    PROPERTIES
      VERSION ${VERSION_SERIES}.${VERSION_MAJOR}.${VERSION_MINOR}
      SOVERSION ${WTISAPI_SOVERSION}
      DEBUG_POSTFIX ${DEBUG_LIB_POSTFIX}
    )
    IF(MSVC)
      SET_TARGET_PROPERTIES(
          wtisapi
        PROPERTIES
          COMPILE_FLAGS "${BUILD_PARALLEL} /wd4267 /wd4251 /wd4275"
          COMPILE_PDB_NAME "wtisapi"
          COMPILE_PDB_NAME_DEBUG "wtisapi${DEBUG_LIB_POSTFIX}"
      )
    ENDIF(MSVC)

    IF(NOT CMAKE_VERSION VERSION_LESS 2.8.12)
      SET_TARGET_PROPERTIES(wtisapi PROPERTIES EXPORT_NAME Isapi)
      SET(_EXPORT_wt_target_isapi EXPORT wt-target-isapi)
    ELSE()
      SET(_EXPORT_wt_target_isapi)
    ENDIF()

    INSTALL(TARGETS wtisapi
      ${_EXPORT_wt_target_isapi}
      RUNTIME DESTINATION bin
      LIBRARY DESTINATION ${LIB_INSTALL_DIR}
      ARCHIVE DESTINATION ${LIB_INSTALL_DIR})

    IF(NOT CMAKE_VERSION VERSION_LESS 2.8.12)
      INSTALL(EXPORT wt-target-isapi
        DESTINATION ${CMAKE_INSTALL_DIR}/wt
        NAMESPACE Wt::)
    ENDIF()
    UNSET(_EXPORT_wt_target_isapi)

  ENDIF(NOT MULTI_THREADED)

ELSE(CONNECTOR_ISAPI)
  MESSAGE("** Disabling ISAPI.")
ENDIF(CONNECTOR_ISAPI)

